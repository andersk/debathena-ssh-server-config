# Warn the user if they log in without tickets or tokens

# Check to make sure the user does not have a local account
if ("$DEBATHENA_HOME_TYPE" == "afs") then
    # Check to make sure the logon is interactive
    if ($?prompt) then
        set tickets=""
        set tokens=""

        # Check for tickets
        klist -s
        if ("$?" == 0) then
            set tickets="yes"
        endif

        # Check for tokens
        tokens | grep -q 'athena\.mit\.edu'
        if ("$?" == 0) then
            set tokens="yes"
        endif

        if ("$tokens" != "yes" && "$tickets" != "yes" && ! -r ~/.cshrc) then
            echo "WARNING: You have no valid Kerberos tickets and no valid AFS tokens." >/dev/stderr
            echo "See http://debathena.mit.edu/ssh" >/dev/stderr
        else if ("$tokens" != "yes" && ! -r ~/.cshrc) then
            echo "WARNING: You have no valid AFS tokens." >/dev/stderr
            echo "See http://debathena.mit.edu/ssh" >/dev/stderr
        else if ("$tickets" != "yes") then
            echo "WARNING: You have no valid Kerberos tickets." >/dev/stderr
            echo "See http://debathena.mit.edu/ssh" >/dev/stderr
        endif
    endif
endif
